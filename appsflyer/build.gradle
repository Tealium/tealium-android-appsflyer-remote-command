apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    namespace = "com.tealium.remotecommands.appsflyer"
    compileSdk 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        buildConfigField 'String', 'TAG', "\"Tealium-AppsFlyer-$tealium_appsflyer_version\""
        buildConfigField 'String', "TEALIUM_APPSFLYER_VERSION", "\"$tealium_appsflyer_version\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig = true
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.tealium.remotecommands'
            artifactId 'appsflyer'
            version tealium_appsflyer_version
            afterEvaluate {
                from components.release
            }
        }
    }

//        repositories {
//            maven {
//                url "s3://maven.tealiumiq.com/android/releases/"
//                credentials(AwsCredentials) {
//                    accessKey AWS_ACCESS_KEY
//                    secretKey AWS_SECRET_KEY
//                    sessionToken AWS_SESSION_TOKEN
//                }
//            }
//        }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Tealium dependencies
    api 'com.tealium:remotecommands:1.0.1'

    // AppsFlyer dependencies
    api "com.appsflyer:af-android-sdk:$appsflyer_version"

    // InstallReferrer dependencies
    implementation 'com.android.installreferrer:installreferrer:2.2'

    // Test dependencies
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.13"
    testImplementation "io.mockk:mockk:$mockk_version"
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    androidTestImplementation "io.mockk:mockk-android:$mockk_version"
}

